# 使用 VBA 代码将 Excel 转换为 JSON

> 原文:[https://www . javatpoint . com/convert-excel-to-JSON-using-VBA-code](https://www.javatpoint.com/convert-excel-to-json-using-vba-code)

除了下载 Excel 到 JSON 转换软件，还可以使用 VBA 代码编辑器将 Excel 数据转换成 JSON 格式。您可以在 VBA 编辑器上创建自己的定制代码，并运行它来完成任务。

VBA 代码编辑器是一个开发工具或 Excel 的内置功能。当你下载微软 Excel 时，它是默认的。在 VBA 代码编辑器中，您可以编写自己的代码并执行特定的任务。如果你对编码感到满意，那么这是一个很好的方法。

通过将 Excel 列映射到 JSON 对象键，制作一个 Excel 到 JSON 转换的代码。然后执行代码将数据转换成 JSON 格式。了解视觉基础编码的人可以使用这个工具。

## 为什么是 VBA 代码编辑器？

如果你擅长编码和创建逻辑代码，那么为什么要选择工具。在 VBA 代码编辑器中创建您自己的定制代码，并通过运行该代码将任意数量的 Excel 文档转换为 JSON 格式。它不需要下载或互联网连接等等。

您不需要显式下载任何工具来将您的 Excel 数据转换为 JSON 格式。虽然你可以做到这一点，而无需下载任何软件在您的系统上使用 Excel VBA 代码编辑器。如果你对编码感到满意，那么这是一个很好的方法。甚至你也不需要安装 VBA 编辑器，因为默认情况下它是随 Excel 一起提供的。

#### 提示:您的 Excel 数据必须有列名，因为第一行总是被视为标题。

### 打开 VBA 编辑器

1.  您可以从“开发人员”选项卡打开 Excel 中的 VBA 代码编辑器。
2.  如果开发人员选项卡在 Excel 功能区中不可用，请通过右键单击功能区并从列表中选择开发人员选项卡来自定义功能区。
3.  然后，点击第一个选项 **Visual Basic** 打开 VBA 代码编辑器。
4.  在 VBA 代码编辑器中，在当前打开的 Excel 文件下创建一个新模块。
5.  在这里编写 Excel 到 JSON 的转换代码。

### VBA 法典

以下是将 Excel 文件数据转换为 JSON 格式的完整代码。复制以下代码，并将其粘贴到您各自的 VBA 代码编辑器中。

```

Public Function ExcelToJSON(rng As Range) As String
    ' Check there must be at least two columns in the Excel file
    If rng.Columns.Count < 2 Then
        ExcelToJSON = CVErr(xlErrNA)
        Exit Function
    End If
    Dim dataLoop, headerLoop As Long
    ' Get the first row of the Excel file as a header 
    Dim headerRange As Range: Set headerRange = Range(rng.Rows(1).Address)   
    ' Count the number of columns of targeted Excel file
    Dim colCount As Long: colCount = headerRange.Columns.Count   
    Dim JSON As String: JSON = "["    
    For dataLoop = 1 To rng.Rows.Count
        ' Skip the first row of the Excel file because it is used as header
        If dataLoop > 1 Then
            ' Start data row
            Dim jsonData As String: jsonData = "{"            
            ' Loop through each column and combine with the header
            For headerLoop = 1 To colCount
                jsonData = jsonData & """" & headerRange.Value2(1, headerLoop) & """" & ":"
                jsonData = jsonData & """" & rng.Value2(dataLoop, headerLoop) & """"
                jsonData = jsonData & ","
            Next headerLoop            
            ' Strip out the comma in last value of each row
            jsonData = Left(jsonData, Len(jsonData) - 1)            
            ' End data row
            JSON = JSON & jsonData & "},"
        End If
    Next    
    ' Strip out the last comma in last row of the Excel data
    JSON = Left(JSON, Len(JSON) - 1)   
    JSON = JSON & "]"    
    ExcelToJSON = JSON
End Function

```

在这段 VBA 代码中，我们定义了一个名为 **ExcelToJSON** 的函数，该函数稍后将在 Excel 文件中用于将 Excel 转换为 JSON。让我们看看它将如何执行。

**语法**

```

=ExcelToJSON(range)

```

此函数将采用 Excel 单元格的范围，例如，您想要转换为 JSON 格式的 A1:F9。

## 将 Excel 数据转换为 JSON 格式

现在，我们将使用我们使用上述代码创建的函数 **ExcelToJSON()** 将 Excel 数据转换为 JSON 格式。记住一件事——这个函数只为这个特定的文件创建。它不适用于其他 Excel 文件。

1.  回到 Excel 文件，在要放置转换后的 JSON 结果的 Excel 单元格中编写以下 ROUND()公式。例如:
    = ExcelToJSON(A1:D3)
    ![Convert the Excel to JSON using VBA code](../Images/d3bf9fe0ed5c1a2c41e7fe99e625b417.png)
    目前我们只将 Excel 前两行四列数据转换为 JSON 格式。
2.  按**进入**键，得到结果，看到数据成功转换成 JSON 形式。
    ![Convert the Excel to JSON using VBA code](../Images/e4e897c7b9b01146eff9875df6284cc0.png)
    同样，通过增加 Excel 单元格的范围，可以使用该功能进行更多的数据转换。从这里复制转换后的 JSON 数据，并将其存储为简单的 Word 文件。

### 代码解释

**代码 1**

```

If rng.Columns.Count < 2 Then
        ExcelToJSON = CVErr(xlErrNA)
        Exit Function
End If

```

首先，该代码用于验证所选范围必须至少有两列，才能进行 Excel 到 JSON 的转换。否则，您将退出该功能。

**代码 2**

```

Dim dataLoop, headerLoop As Long
' Get the first row of the Excel file as a header 
Dim headerRange As Range: Set headerRange = Range(rng.Rows(1).Address)

```

接下来，我们创建了两个变量:数据循环和头循环。存储在 Excel 文件中的 Excel 数据的数据循环变量，以及列标题的数据循环变量。这两个变量都将在转换过程中使用。

**代码 3**

```

' Count the number of columns of targeted Excel file
Dim colCount As Long: colCount = headerRange.Columns.Count

```

这里，我们已经创建了一个变量 **colCount。**我们已经使用了这段代码来查找目标列的数量，以将其数据转换为 Excel 文件中的 JSON 格式。

**代码 4**

```

Dim JSON As String: JSON = "["   
For dataLoop = 1 To rng.Rows.Count
       ' Skip the first row because it is used as header
        If dataLoop > 1 Then
             ' Start data row
             Dim jsonData As String: jsonData = "{"

```

现在，这段代码开始从 Excel 文件创建 JSON 数据。这里，dataLoop 变量用在从 1 到所选行末尾的 for 循环中。Excel 文件数据的第一行将被跳过，因为在将 Excel 数据转换为 JSON 时，第一行总是被视为标题。

JSON 格式的数据将被括在大括号{}之间。

**代码 5**

```

            ' Loop through each column and combine with the header
            For headerLoop = 1 To colCount
                jsonData = jsonData & """" & headerRange.Value2(1, headerLoop) & """" & ":"
                jsonData = jsonData & """" & rng.Value2(dataLoop, headerLoop) & """"
                jsonData = jsonData & ","
            Next headerLoop            
            ' Strip out the comma in last value of each row
            jsonData = Left(jsonData, Len(jsonData) - 1)            
            ' End data row
            JSON = JSON & jsonData & "},"
        End If

```

循环将贯穿每一列，并将列标题与行数据组合在一起。每个值将由逗号分隔。而这段代码( **jsonData = Left(jsonData，Len(jsonData) - 1)** )将跳过每行后最后一个值中的逗号。

在 JSON 文件格式中，每行数据都将包含在{}个大括号中。

**代码 5**

```

    ' Strip out the last comma in last row of the Excel data
    JSON = Left(JSON, Len(JSON) - 1)    
    JSON = JSON & "]"    
    ExcelToJSON = JSON

```

所有串联的数据将存储在一个 **JSON** 名称变量中，这个完整的数据将放在[]方括号之间。转换后的 JSON 数据将在一个 Excel 单元格中显示给用户，用户将在该单元格中使用这个用户定义的 ExcelToJSON()函数。

* * *